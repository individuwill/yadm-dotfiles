# ~/.myprofile

bindkey -e

# ---- Basics ----
export EDITOR="vim"
export SSH_AUTH_SOCK="$HOME/.1password/agent.sock"

export DOTNET_NEW_PREFERRED_LANG="F#"
export DOTNET_ROOT="/usr/local/share/dotnet"
export PAGER="most"

# Aliases
alias sssh="ssh -o StrictHostKeyChecking=no -o UserKnownHostsFile=/dev/null"
alias oe="op run --env-file=.env -- "
alias oel="op run --env-file=.env.local -- "
alias yt="youtube-dl --external-downloader aria2c --external-downloader-args '-c -j 3 -x 3 -s 3 -k 1M'"
alias ls="lsd"
# alias vim="nvim"
alias flushdns="sudo dscacheutil -flushcache; sudo killall -HUP mDNSResponder"

# ---- Deterministic base PATH (zsh-native, deduped) ----
export NVM_DIR="$HOME/.nvm"

typeset -U path PATH
path=(
  "$HOME/.config/emacs/bin"
  "/Applications/WezTerm.app/Contents/MacOS"
  "$HOME/.cargo/bin"
  "$HOME/.cache/rebar3/bin"
  "/opt/homebrew/opt/coreutils/libexec/gnubin"
  "$HOME/.rd/bin"
  "$HOME/google-cloud-sdk/bin"
  "$HOME/.poetry/bin"
  "$HOME/.local/bin"
  "$HOME/bin"
  "$DOTNET_ROOT"
  "/usr/local/go/bin"
  "$HOME/go/bin"
  "/opt/homebrew/bin"
  "/opt/homebrew/sbin"
  "$HOME/.lmstudio/bin"
  "$HOME/.antigravity/antigravity/bin"
  "$HOME/Application Support/JetBrains/Toolbox/scripts"
  $path
)
export PATH

# ---- Tool inits ----

# opam
[[ ! -r "$HOME/.opam/opam-init/init.zsh" ]] || source "$HOME/.opam/opam-init/init.zsh" > /dev/null 2> /dev/null

# direnv
eval "$(direnv hook zsh)"

# Completions that require zsh completion system (compinit) should be interactive-only
if [[ -o interactive ]]; then
  source <(kubectl completion zsh)

  # UV Python
  eval "$(uv generate-shell-completion zsh)"
  eval "$(uvx --generate-shell-completion zsh)"
fi

# Cargo (may tweak PATH)
. "$HOME/.cargo/env"

# nvm (may tweak PATH)
if [[ -s "$NVM_DIR/nvm.sh" ]]; then
  . "$NVM_DIR/nvm.sh"

  # Make node selection stable in interactive shells (so PATH + starship modules match)
  if [[ -o interactive ]]; then
    nvm use --silent default >/dev/null 2>&1 || true
  fi
fi

# luaver (may tweak PATH)
if command -v luaver >/dev/null 2>&1; then
  . "$(command -v luaver)"
fi

# orbstack (may tweak PATH)
source "$HOME/.orbstack/shell/init.zsh" 2>/dev/null || :

### Begin private shell env setup ###
PRIVATE_DIR="$HOME/.config/shell"
PRIVATE_ENV="$PRIVATE_DIR/private_env.sh"
GITIGNORE="$PRIVATE_DIR/.gitignore"

mkdir -p "$PRIVATE_DIR"

# Create/ensure .gitignore that keeps the directory but ignores other contents
if [[ ! -f "$GITIGNORE" ]]; then
  cat >"$GITIGNORE" <<'EOF'
# Keep this directory in git, but ignore its contents (secrets, local files)
*
!.gitignore
EOF
fi

# Create the env file if missing; lock down perms
if [[ ! -f "$PRIVATE_ENV" ]]; then
  touch "$PRIVATE_ENV"
  chmod 600 "$PRIVATE_ENV"
fi

# Source if readable
if [[ -r "$PRIVATE_ENV" ]]; then
  # shellcheck disable=SC1090
  source "$PRIVATE_ENV"
fi

### End private shell env setup ###

# Local overrides (may tweak PATH, env, etc.)
[[ -r "$HOME/.myprofile.local" ]] && source "$HOME/.myprofile.local"

# ---- FINAL PATH CANONICALIZATION (force identical ordering) ----
typeset -U path PATH

# Remove pinned entries anywhere they already exist
path=(${path:#/opt/homebrew/bin})
path=(${path:#/opt/homebrew/sbin})
path=(${path:#$HOME/.luarocks/bin})

# Prefer nvm's currently-selected node (if available)
nvm_node_bin=""
if [[ -s "$NVM_DIR/nvm.sh" ]]; then
  # If nvm is loaded, this points at the selected version
  nvm_node_bin="$(nvm which current 2>/dev/null)"
  [[ "$nvm_node_bin" == "none" || -z "$nvm_node_bin" ]] && nvm_node_bin=""
  nvm_node_bin="${nvm_node_bin%/*}"
fi

front=(
  "$nvm_node_bin"
  "$HOME/.luarocks/bin"
  "/opt/homebrew/bin"
  "/opt/homebrew/sbin"
)

# Drop empty entries (if nvm isn't active)
front=(${front:#""})

path=($front $path)
export PATH

# ---- Optional: auto-start zellij only when not already inside it ----
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  :
elif [[ "$TERM_PROGRAM" == "zed" ]]; then
  :
elif [[ "$TERMINAL_EMULATOR" == "JetBrains-JediTerm" ]]; then
  :
else
  if [[ -z "$ZELLIJ" && -o interactive && -z "$NO_ZELLIJ" ]]; then
    # exec zellij -l welcome
    :
  fi
fi

source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

